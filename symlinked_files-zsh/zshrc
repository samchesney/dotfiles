# Pure prompt support
if [ -d $HOME/.dotfiles/zsh_plugins/pure ]; then
  fpath+=$HOME/.dotfiles/zsh_plugins/pure
  autoload -Uz promptinit
  promptinit

  # Customise pure prompt
  PURE_PROMPT_SYMBOL="%%"
  PURE_GIT_PULL=0
  zstyle :prompt:pure:git:stash show yes

  prompt pure
fi

# Autocomplete support
# Usually Brew installs completions in a location which macOS adds to the fpath
# by default so only add it when necessary
if [ $(command -v brew) ]; then
  brew_fpath=$(brew --prefix)/share/zsh/site-functions
  if [[ ${fpath[(i)$brew_fpath]} -ge ${#fpath} ]] ; then
    fpath=($brew_fpath $fpath)
  fi
  unset brew_fpath
fi

if [ -d $HOME/.zfunc ]; then
  fpath+=$HOME/.zfunc
fi

autoload -Uz compinit
compinit

# Set up fzf key bindings and fuzzy completion
if [ $(command -v fzf) ]; then
  eval "$(fzf --zsh)"

  # Note fzf-tab needs to be loaded after compinit, but before plugins which
  # will wrap widgets, such as zsh-autosuggestions or fast-syntax-highlighting
  if [ -f $HOME/.dotfiles/zsh_plugins/fzf-tab/fzf-tab.plugin.zsh ]; then
    source $HOME/.dotfiles/zsh_plugins/fzf-tab/fzf-tab.plugin.zsh
  fi

  # disable sort when completing `git checkout`
  zstyle ':completion:*:git-checkout:*' sort false
  # set descriptions format to enable group support
  # NOTE: don't use escape sequences (like '%F{red}%d%f') here, fzf-tab will ignore them
  zstyle ':completion:*:descriptions' format '[%d]'
  # set list-colors to enable filename colorizing
  zstyle ':completion:*' list-colors ${(s.:.)LS_COLORS}
  # force zsh not to show completion menu, which allows fzf-tab to capture the unambiguous prefix
  zstyle ':completion:*' menu no
  # preview directory's content with eza when completing cd
  zstyle ':fzf-tab:complete:cd:*' fzf-preview 'eza -1 --color=always $realpath'
  # custom fzf flags
  # NOTE: fzf-tab does not follow FZF_DEFAULT_OPTS by default
  zstyle ':fzf-tab:*' fzf-flags --color=fg:1,fg+:2 --bind=tab:accept
  # To make fzf-tab follow FZF_DEFAULT_OPTS.
  # NOTE: This may lead to unexpected behavior since some flags break this plugin. See Aloxaf/fzf-tab#455.
  zstyle ':fzf-tab:*' use-fzf-default-opts yes
  # switch group using `<` and `>`
  zstyle ':fzf-tab:*' switch-group '<' '>'
fi

if [ -f $HOME/.dotfiles/zsh_plugins/zsh-autosuggestions/zsh-autosuggestions.zsh ]; then
  source $HOME/.dotfiles/zsh_plugins/zsh-autosuggestions/zsh-autosuggestions.zsh
fi

if [ -f $HOME/.zshrc_extra ]; then
  source $HOME/.zshrc_extra
fi

if [ -f $HOME/.interactive ]; then
  source $HOME/.interactive
fi

# Syntax highlighting support
# Note this must be sourced at the end of .zshrc
if [ -f $HOME/.dotfiles/zsh_plugins/zsh-syntax-highlighting/zsh-syntax-highlighting.zsh ]; then
  source $HOME/.dotfiles/zsh_plugins/zsh-syntax-highlighting/zsh-syntax-highlighting.zsh
fi
